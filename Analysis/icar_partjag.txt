model
{
    gamma0 ~ dnorm(0.00000E+00, 1.00000E-06)
    beta ~ dunif(0.00000E+00, 2)
    alpha ~ dunif(50, 200)
    K ~ dunif(0.00000E+00, 0.5)
    sd.rho ~ dunif(0.00000E+00, 10)
    tau.rho <- pow(sd.rho, -2)
    rho[1] ~ dnorm(0.00000E+00, 1.00000E-06)
    for (i in 2:D) {
        rho[i] ~ dnorm(rho[i - 1], tau.rho)
    }
    for (j in 1:records) {
        for (k in left[j]:(right[j] - 1)) {
            UCH[j, k] <- exp(gamma0 + rho[k])
        }
        SLR[j] <- exp(-sum(UCH[j, left[j]:(right[j] - 1)]))
        noevent[j] ~ dbern(SLR[j])
    }
    for (m in 1:n.events) {
        b.type[m] ~ dbern(p.a[m])
        p.a[m] <- 1 + (K - 1)/(1 + exp(-beta * (event.day[m] - 
            alpha)))
    }
    for (i in 1:D) {
        Haz_b[i] <- exp(gamma0 + rho[i])
        prob.ab[i] <- 1 + (K - 1)/(1 + exp(-beta * (i - alpha)))
        Haz_a[i] <- prob.ab[i] * Haz_b[i]
        Haz_l[i] <- (1 - prob.ab[i]) * Haz_b[i]
    }
    CumHaz_b <- sum(Haz_b[1:D])
    CumHaz_a <- sum(Haz_a[1:D])
    CumHaz_l <- sum(Haz_l[1:D])
}
